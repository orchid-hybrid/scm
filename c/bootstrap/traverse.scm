;; Traversal functions

(define (variable-form? t)
  (symbol? t))

(define (lambda-form? t)
  (and (list? t)
       (= 3 (length t))
       (equal? 'lambda (car t))
       (list? (cadr t))
       (all variable-form? (cadr t))))

(define (lambda*-form? t)
  (and (list? t)
       (= 4 (length t))
       (equal? 'lambda* (car t))
       (list? (cadr t))
       (all variable-form? (cadr t))
       (all variable-form? (caddr t))))

(define (application-form? t)
  (list? t))

(define (traverse string-case char-case number-case boolean-case quote-case variable-case lambda-case application-case)
  ;; t ::= v
  ;;     | (lambda (v ...) t)
  ;;     | (t ...)
  (lambda (t)
    (cond ((string? t)
           (string-case t))
          ((char? t)
           (char-case t))
          ((number? t)
           (number-case t))
          ((boolean? t)
           (boolean-case t))
          ((and (list? t)
                (= (length t) 2)
                (equal? (car t) 'quote))
           (quote-case (cadr t)))
          ((variable-form? t)
           (variable-case t))
          ((lambda-form? t)
           (lambda-case (cadr t)
                        (caddr t)))
          ((application-form? t)
           (application-case t))
          (else (error "traverse")))))

(define (traverse* string-case char-case number-case boolean-case quote-case variable-case lambda*-case application-case)
  ;; f ::= v
  ;;     | (lambda* (v ...) (v ...) f)
  ;;                        ^^^^^^--- free variables added here
  ;;     | (f ...)
  (lambda (t)
    (cond ((string? t)
           (string-case t))
          ((char? t)
           (char-case t))
          ((number? t)
           (number-case t))
          ((boolean? t)
           (boolean-case t))
          ((and (list? t)
                (= (length t) 2)
                (equal? (car t) 'quote))
           (quote-case (cadr t)))
          ((variable-form? t)
           (variable-case t))
          ((lambda*-form? t)
           (lambda*-case (cadr t)
                         (caddr t)
                         (cadddr t)))
          ((application-form? t)
           (application-case t))
          (else (error "traverse*")))))

